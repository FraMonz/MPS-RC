{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "7f517f79",
   "metadata": {},
   "outputs": [],
   "source": [
    "using ITensors, ITensorMPS\n",
    "\n",
    "let\n",
    "  N = 100\n",
    "  cutoff = 1E-8\n",
    "  tau = 0.1\n",
    "  ttotal = 5.0\n",
    "\n",
    "  # Make an array of 'site' indices\n",
    "  s = siteinds(\"S=1/2\", N; conserve_qns=true)\n",
    "\n",
    "  # Make gates (1,2),(2,3),(3,4),...\n",
    "  gates = ITensor[]\n",
    "  for j in 1:(N - 1)\n",
    "    s1 = s[j]\n",
    "    s2 = s[j + 1]\n",
    "    hj =\n",
    "      op(\"Sz\", s1) * op(\"Sz\", s2) +\n",
    "      1 / 2 * op(\"S+\", s1) * op(\"S-\", s2) +\n",
    "      1 / 2 * op(\"S-\", s1) * op(\"S+\", s2)\n",
    "    Gj = exp(-im * tau / 2 * hj)\n",
    "    push!(gates, Gj)\n",
    "  end\n",
    "  # Include gates in reverse order too\n",
    "  # (N,N-1),(N-1,N-2),...\n",
    "  append!(gates, reverse(gates))\n",
    "\n",
    "  # Initialize psi to be a product state (alternating up and down)\n",
    "  psi = MPS(s, n -> isodd(n) ? \"Up\" : \"Dn\")\n",
    "\n",
    "  c = div(N, 2) # center site\n",
    "\n",
    "  # Compute and print <Sz> at each time step\n",
    "  # then apply the gates to go to the next time\n",
    "  for t in 0.0:tau:ttotal\n",
    "    Sz = expect(psi, \"Sz\"; sites=c)\n",
    "    println(\"$t $Sz\")\n",
    "\n",
    "    tâ‰ˆttotal && break\n",
    "\n",
    "    psi = apply(gates, psi; cutoff)\n",
    "    normalize!(psi)\n",
    "  end\n",
    "\n",
    "  return\n",
    "end"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.12.2",
   "language": "julia",
   "name": "julia-1.12"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.12.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
